set dotenv-load := false

COLOR := "\\033[0;32m"
NO_COLOR := "\\033[0m"

# Show all available recipes
@_default:
    printf "\n{{ COLOR }}# Frontend (path: \`frontend/\`)\n"
    printf "=============================={{ NO_COLOR }}\n"
    just --list --unsorted


###########
# Version #
###########

export FRONTEND_NODE_VERSION := `grep "volta" -A 1 ../package.json | tail -n 1 | awk -F'[:"]' '{print $5}' | cut -d. -f1`
export FRONTEND_PNPM_VERSION := `grep 'packageManager' ../package.json | awk -F'@' '{print $2}' | sed 's/",//g'`

# Print the Node.js version specified in `package.json`
@node-version:
    echo $FRONTEND_NODE_VERSION

# Print the pnpm version specified in `package.json`
@pnpm-version:
    echo $FRONTEND_PNPM_VERSION

##########
# Docker #
##########

# This section exists because the frontend is not a part of the Docker-based setup
# and cannot be built easily like `just dc build <service>`.

# Build the frontend Docker image
build-img tag="openverse-frontend:local" target="app":
    docker buildx build \
    --target {{ target }} \
    --build-context 'repo_root=..' \
    --build-arg FRONTEND_NODE_VERSION=$(just node-version) \
    --build-arg FRONTEND_PNPM_VERSION=$(just pnpm-version) \
    --tag {{ tag }} \
    .

######
# Up #
######

# Bring up services specific to the frontend profile
up *flags:
    env COMPOSE_PROFILES="frontend" just ../up {{ flags }}

# Wait for all profile services to be up
wait-up: up
    echo "ðŸš§ TODO"

# Set up user and test site in Plausible
init: wait-up
    cd .. && ./setup_plausible.sh

# Run a package.json script via pnpm
run *args:
    pnpm run {{ args }}

types:
    cd .. && pnpm exec vue-tsc -p frontend --noEmit
